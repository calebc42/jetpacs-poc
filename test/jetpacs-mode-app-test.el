;;; jetpacs-mode-app-test.el --- ERT for reader/editor mode apps -*- lexical-binding: t; -*-

;; SPDX-License-Identifier: GPL-3.0-or-later

(require 'ert)
(require 'cl-lib)
(require 'org)
(require 'jetpacs-org-mode)

(defmacro jetpacs-mode-app-test--with-org-file (binding content &rest body)
  "Bind BINDING to a temporary Org file containing CONTENT during BODY."
  (declare (indent 2) (debug (symbolp form body)))
  `(let ((,binding (make-temp-file "jetpacs-mode-app-" nil ".org"
                                    ,content)))
     (unwind-protect
         (progn ,@body)
       (when-let* ((buffer (get-file-buffer ,binding)))
         (with-current-buffer buffer (set-buffer-modified-p nil))
         (kill-buffer buffer))
       (delete-file ,binding))))

(defun jetpacs-mode-app-test--toolbar-commands (items)
  "Return every command string nested one level below toolbar ITEMS."
  (cl-loop for item in items
           append
           (append
            (when-let* ((command (plist-get item :command)))
              (list command))
            (when-let* ((menu (plist-get item :menu)))
              (jetpacs-mode-app-test--toolbar-commands
               (append menu nil))))))

(defun jetpacs-mode-app-test--toolbar-snippets (items)
  "Return every snippet string nested below toolbar ITEMS."
  (cl-loop for item in items
           append
           (append
            (when-let* ((snippet (plist-get item :snippet)))
              (list snippet))
            (when-let* ((menu (plist-get item :menu)))
              (jetpacs-mode-app-test--toolbar-snippets
               (append menu nil))))))

(ert-deftest jetpacs-reader-org-path-p-includes-native-archives ()
  "Org's sibling archive convention selects the same native adapter."
  (dolist (path '("/tmp/note.org" "/tmp/note.ORG"
                  "/tmp/note.org_archive" "/tmp/note.ORG_ARCHIVE"))
    (should (jetpacs-reader-org-path-p path)))
  (dolist (path '("/tmp/note.org_archive.bak" "/tmp/note_archive"
                  "/tmp/note.orgx_archive" nil))
    (should-not (jetpacs-reader-org-path-p path)))
  (should (jetpacs-reader-adapter-for "/tmp/note.org_archive")))

(ert-deftest jetpacs-reader-registry-and-toggle-are-document-scoped ()
  "The generic host selects an adapter and refuses a replay for another file."
  (jetpacs-mode-app-test--with-org-file file "* One\n"
    (let ((jetpacs-reader--adapters nil)
          (jetpacs-reader--state (make-hash-table :test #'equal))
          (jetpacs-files--edit (list :path file))
          refreshed)
      (jetpacs-reader-register
       'fixture
       :predicate (lambda (path) (string-suffix-p ".org" path))
       :render (lambda (_path) (jetpacs-text "rendered")))
      (should (jetpacs-reader-active-p file))
      (cl-letf (((symbol-function 'jetpacs-buffer-defer-refresh)
                 (lambda (surface) (setq refreshed surface))))
        (should (eq (jetpacs-reader--toggle
                     (list :path file) '(:surface "app:files"))
                    'accepted))
        (should-not (jetpacs-reader-active-p file))
        (should (equal refreshed "app:files"))
        (should (eq (jetpacs-reader--toggle
                     '(:path "/tmp/not-the-open-file.org")
                     '(:surface "app:files"))
                    'stale))))))

(ert-deftest jetpacs-reader-org-register-reasserts-replaced-slot ()
  "An already-registered stock adapter can be restored after replacement."
  (let ((jetpacs-reader--adapters nil)
        ;; Model the real GR-2 lifecycle: stock actions remain installed
        ;; while another app temporarily owns adapter id `org'.
        (jetpacs-reader-org--registered t))
    (jetpacs-reader-register
     'org :predicate #'jetpacs-reader-org-path-p
     :render (lambda (_path) (jetpacs-text "replacement"))
     :actions #'ignore :transition #'ignore)
    (jetpacs-reader-org-register)
    (let ((adapter (jetpacs-reader-adapter-for "/tmp/restored.org")))
      (should (eq (jetpacs-reader-adapter-render adapter)
                  #'jetpacs-reader-org--render))
      (should (eq (jetpacs-reader-adapter-actions adapter)
                  #'jetpacs-reader-org--actions))
      (should (eq (jetpacs-reader-adapter-transition adapter)
                  #'jetpacs-reader-org--transition)))))

(ert-deftest jetpacs-reader-org-rents-built-in-search-and-visibility ()
  "Plain/regexp search uses org-occur; sparse filters use Org's matcher."
  (jetpacs-mode-app-test--with-org-file
      file "* TODO Alpha :work:\nNeedle one\n* Beta :home:\nNeedle two\n"
    (let ((jetpacs-reader--state (make-hash-table :test #'equal)))
      (should (= (jetpacs-reader-org--apply-search file "Needle") 2))
      (with-current-buffer (get-file-buffer file)
        (should (= (length org-occur-highlights) 2)))
      (jetpacs-reader-state-set file :org-search-mode 'regexp)
      (should (= (jetpacs-reader-org--apply-search file "Needle \\(one\\|two\\)")
                 2))
      (jetpacs-reader-state-set file :org-search-mode 'sparse)
      (should (eq (jetpacs-reader-org--apply-search file "+work") 'sparse))
      (should-error (progn
                      (jetpacs-reader-state-set file :org-search-mode 'regexp)
                      (jetpacs-reader-org--apply-search file "["))
                    :type 'user-error)
      (jetpacs-reader-state-set file :org-visibility 'contents)
      (jetpacs-reader-org--clear-search-in-buffer
       file (jetpacs-reader-org--buffer file))
      (should (eq (jetpacs-reader-org--visibility file) 'contents)))))

(ert-deftest jetpacs-reader-org-reader-mode-is-buffer-local ()
  "Reader mode hides markup and prettifies entities without global mutation."
  (jetpacs-mode-app-test--with-org-file file "* A *bold* \\alpha\n"
    (let ((jetpacs-reader--state (make-hash-table :test #'equal))
          (global-hide (default-value 'org-hide-emphasis-markers)))
      (let ((buffer (jetpacs-reader-org--buffer file)))
        (jetpacs-reader-org--apply-reader-mode file buffer)
        (with-current-buffer buffer
          (should org-hide-emphasis-markers)
          (should org-pretty-entities)
          (should org-hide-leading-stars))
        (should (eq (default-value 'org-hide-emphasis-markers) global-hide))))))

(ert-deftest jetpacs-reader-org-render-scrolls-with-orgro-typography ()
  "The reader scrolls and reflows headings without prescribing a font."
  (jetpacs-mode-app-test--with-org-file
      file (concat "Intro paragraph.\n\n"
                   "* Attachments                                      :ATTACH:\n\n"
                   "* Next\n")
    (let* ((jetpacs-reader--state (make-hash-table :test #'equal))
           (root (jetpacs-reader-org--render file))
           (children (append (plist-get root :children) nil))
           (heading-index
            (seq-position
             children "Attachments"
             (lambda (node needle)
               (string-match-p needle
                               (jetpacs-node->canonical-json node)))))
           (heading (and heading-index (nth heading-index children)))
           (heading-children (append (plist-get heading :children) nil))
           (headline (car heading-children))
           (overflow (cadr heading-children))
           (spans (append (plist-get headline :spans) nil))
           (text (mapconcat (lambda (span) (plist-get span :text)) spans ""))
           (body-span
            (seq-find
             (lambda (span)
               (equal (plist-get (plist-get span :on_tap) :action)
                      "jetpacs.buffer.fold"))
             spans)))
      (should (equal (plist-get root :t) "lazy_column"))
      (should heading-index)
      (should (equal (plist-get heading :t) "row"))
      (should (equal (plist-get overflow :icon) "more_vert"))
      (should (equal (plist-get (plist-get overflow :on_tap) :action)
                     "jetpacs.org.heading"))
      (should (= (plist-get body-span :font_weight) 800))
      (should (string-match-p "Attachments :ATTACH:" text))
      (should-not (string-match-p "Attachments  +:ATTACH:" text))
      (should-not (string-match-p "[▸▾]" text))
      (let ((gap (nth (1+ heading-index) children)))
        (should (equal (plist-get gap :t) "spacer"))
        (should (= (plist-get gap :height) 8))))))

(ert-deftest jetpacs-editor-org-commands-require-sync-but-snippets-do-not ()
  "Plain editors get local helpers; synchronized editors also get Org commands."
  (let* ((jetpacs-files-editor-context '(:path "/tmp/a.org"))
         (plain (jetpacs-editor-org--toolbar "/tmp/a.org"))
         (plain-commands (jetpacs-mode-app-test--toolbar-commands plain))
         (snippets (jetpacs-mode-app-test--toolbar-snippets plain)))
    (should-not plain-commands)
    (should (member "_${selection}_" snippets))
    (should (member "_{${selection}}" snippets))
    (should (member "[cite:@${input:Key}]" snippets))
    (let* ((jetpacs-files-editor-context
            '(:path "/tmp/a.org" :document "doc:a.org" :editor-id "body"))
           (synced (jetpacs-editor-org--toolbar "/tmp/a.org"))
           (commands (jetpacs-mode-app-test--toolbar-commands synced)))
      (should (member "org-todo" commands))
      (should (member "org-refile" commands))
      (should (member "jetpacs-editor-org-encrypt-entry" commands)))))

(ert-deftest jetpacs-editor-org-installs-buffer-local-crypt-save-hook ()
  "A live Org editor re-encrypts decrypted crypt entries before save."
  (jetpacs-mode-app-test--with-org-file file "* Secret :crypt:\ntext\n"
    (let ((buffer (find-file-noselect file)))
      (jetpacs-editor-org--setup file)
      (with-current-buffer buffer
        (should (memq #'org-encrypt-entries before-save-hook))))))

(ert-deftest jetpacs-editor-org-prewrite-runs-org-crypt-explicitly ()
  "Files' write-region path invokes Org Crypt without relying on save-buffer."
  (with-temp-buffer
    (org-mode)
    (let (called)
      (cl-letf (((symbol-function 'org-encrypt-entries)
                 (lambda () (setq called (current-buffer)))))
        (jetpacs-editor-org--before-save "/tmp/a.org" (current-buffer))
        (should (eq called (current-buffer)))))))

(ert-deftest jetpacs-editor-org-narrowed-body-is-plain-section-editor ()
  "A narrowed Org buffer edits only its subtree with no sync commands."
  (jetpacs-mode-app-test--with-org-file
      file "* One\nfirst\n* Two\nsecond\n"
    (let ((jetpacs-reader--state (make-hash-table :test #'equal))
          (jetpacs-files-editor-context
           (list :path file :mtime "stamp"
                 :document "doc:test.org" :editor-id "body")))
      (let ((buffer (jetpacs-reader-org--buffer file)))
        (with-current-buffer buffer
          (goto-char (point-min))
          (org-narrow-to-subtree))
        (jetpacs-reader-state-set file :presentation 'editor)
        (let* ((node (jetpacs-editor-org--body file))
               (commands
                (jetpacs-mode-app-test--toolbar-commands
                 (append (plist-get node :toolbar) nil))))
          (should (equal (plist-get node :t) "editor"))
          (should-not (plist-get node :document))
          (should (string-search "* One\nfirst" (plist-get node :value)))
          (should-not (string-search "* Two" (plist-get node :value)))
          (should-not commands)
          (should (equal (plist-get (plist-get node :on_save) :action)
                         "jetpacs.editor.org.save-narrowed")))))))

(ert-deftest jetpacs-reader-org-narrow-transition-downgrades-sync ()
  "Switching a narrowed reader to edit removes whole-document sync identity."
  (jetpacs-mode-app-test--with-org-file file "* One\nbody\n* Two\nrest\n"
    (let* ((buffer (jetpacs-reader-org--buffer file))
           (jetpacs-files--edit
            (list :path (file-truename file) :seed "stale"
                  :mtime "stamp" :document "doc:test.org"
                  :editor-id "body" :buffer buffer)))
      (with-current-buffer buffer
        (goto-char (point-min))
        (org-narrow-to-subtree))
      (jetpacs-reader-org--transition file 'editor)
      (should-not (plist-get jetpacs-files--edit :document))
      (should-not (plist-get jetpacs-files--edit :buffer))
      (should (string-search "* Two\nrest"
                             (plist-get jetpacs-files--edit :seed))))))

(ert-deftest jetpacs-editor-org-narrowed-save-splices-only-section ()
  "A validated section save preserves the rest of the Org file."
  (jetpacs-mode-app-test--with-org-file
      file "* One\nfirst\n* Two\nsecond\n"
    (let* ((true (file-truename file))
           (root (file-name-directory true))
           (jetpacs-files-roots (list root))
           (jetpacs-reader--state (make-hash-table :test #'equal))
           (buffer (jetpacs-reader-org--buffer true))
           (jetpacs-files--edit
            (list :path true :seed "" :mtime (jetpacs-files--mtime-stamp true)
                  :coding nil)))
      (with-current-buffer buffer
        (goto-char (point-min))
        (org-narrow-to-subtree))
      (jetpacs-reader-state-set true :presentation 'editor)
      ;; The body records the Emacs-owned bounds and modification tick.
      (let ((jetpacs-files-editor-context jetpacs-files--edit))
        (jetpacs-editor-org--body true))
      (cl-letf (((symbol-function 'jetpacs-shell-notify) #'ignore)
                ((symbol-function 'jetpacs-buffer-defer-refresh) #'ignore))
        (should
         (eq (jetpacs-editor-org--save-narrowed
              (list :path true :mtime (jetpacs-files--mtime-stamp true)
                    :value "* One\nchanged")
              '(:surface "app:jetpacs.files"))
             'accepted)))
      (with-temp-buffer
        (insert-file-contents true)
        (should (equal (buffer-string)
                       "* One\nchanged\n* Two\nsecond\n")))
      (with-current-buffer buffer
        (should (buffer-narrowed-p))
        ;; `org-narrow-to-subtree' excludes the separator newline before
        ;; the next heading; the section editor preserves those exact
        ;; accessible bounds.
        (should (equal (buffer-string) "* One\nchanged"))))))

(ert-deftest jetpacs-editor-org-narrowed-save-rolls-back-before-write-error ()
  "A failed pre-write transform changes neither disk nor visiting buffer."
  (jetpacs-mode-app-test--with-org-file file "* One\nfirst\n* Two\nsecond\n"
    (let* ((true (file-truename file))
           (jetpacs-files-roots (list (file-name-directory true)))
           (jetpacs-reader--state (make-hash-table :test #'equal))
           (buffer (jetpacs-reader-org--buffer true))
           (jetpacs-files--edit
            (list :path true :seed "" :mtime (jetpacs-files--mtime-stamp true)
                  :coding nil)))
      (with-current-buffer buffer
        (goto-char (point-min))
        (org-narrow-to-subtree))
      (jetpacs-reader-state-set true :presentation 'editor)
      (let ((jetpacs-files-editor-context jetpacs-files--edit))
        (jetpacs-editor-org--body true))
      (cl-letf (((symbol-function 'jetpacs-editor--files-before-save)
                 (lambda (&rest _) (error "encryption failed")))
                ((symbol-function 'jetpacs-shell-notify) #'ignore))
        (should
         (eq (jetpacs-editor-org--save-narrowed
              (list :path true :mtime (jetpacs-files--mtime-stamp true)
                    :value "* One\nclear text\n")
              '(:surface "app:jetpacs.files"))
             'rejected)))
      (with-temp-buffer
        (insert-file-contents true)
        (should (equal (buffer-string)
                       "* One\nfirst\n* Two\nsecond\n")))
      (with-current-buffer buffer
        (should (equal (buffer-string) "* One\nfirst"))))))

(ert-deftest jetpacs-org-mode-reminders-filter-time-and-deduplicate ()
  "Timed Agenda rows become alarms; duplicate schedule/deadline rows do not."
  (let* ((now (encode-time 0 0 12 14 8 2026))
         (items
          (list
           '((headline . "Meeting") (file . "/vault/a.org") (pos . 42)
             (time . "13:30......") (date . "2026-08-14")
             (type . "scheduled"))
           '((headline . "Meeting") (file . "/vault/a.org") (pos . 42)
             (time . "13:30......") (date . "2026-08-14")
             (type . "deadline"))
           '((headline . "Date only") (file . "/vault/a.org") (pos . 90)
             (time) (date . "2026-08-14") (type . "scheduled"))
           '((headline . "Too late") (file . "/vault/b.org") (pos . 7)
             (time . "13:30") (date . "2026-08-15")
             (type . "scheduled")))))
    (cl-letf (((symbol-function 'jetpacs-org-mode--agenda-items)
               (lambda (_span &optional _start-day) items)))
      (let ((reminders (jetpacs-org-mode--upcoming-reminders 24 now)))
        (should (= 1 (length reminders)))
        (should (equal (plist-get (car reminders) :title) "Meeting"))
        (should (equal (plist-get (car reminders) :body)
                       "13:30 · scheduled"))
        (should (string-match-p "\\`org-rem-[[:xdigit:]]\\{20\\}\\'"
                                (plist-get (car reminders) :id)))))))

(ert-deftest jetpacs-org-mode-seeds-manual-and-inbox-without-overwrite ()
  "The complete manual bundle lands once and user edits always win."
  (let ((org-directory (make-temp-file "jetpacs-org-seed-" t)))
    (unwind-protect
        (let* ((paths (jetpacs-org-mode-seed))
               (inbox (plist-get paths :inbox))
               (manual (plist-get paths :manual))
               (attachment
                (expand-file-name
                 "data/C2/59CE94-D4C8-4C4F-9C9E-9ABE446E7DA3/hello-world.pdf"
                 (plist-get paths :manual-directory))))
          (should (file-readable-p inbox))
          (should (file-readable-p manual))
          (should (file-readable-p attachment))
          (should (file-readable-p
                   (expand-file-name "LICENSE"
                                     (plist-get paths :manual-directory))))
          (with-temp-file inbox (insert "user inbox\n"))
          (with-temp-file manual (insert "user manual note\n"))
          (jetpacs-org-mode-seed)
          (should (equal (with-temp-buffer
                           (insert-file-contents inbox)
                           (buffer-string))
                         "user inbox\n"))
          (should (equal (with-temp-buffer
                           (insert-file-contents manual)
                           (buffer-string))
                         "user manual note\n")))
      (delete-directory org-directory t))))

(ert-deftest jetpacs-org-mode-open-seed-uses-validated-files-route-and-surface ()
  "Seed navigation is exactly a validated Files open on the Files surface."
  (let ((file (make-temp-file "jetpacs-seed-open-" nil ".org" "* Manual\n"))
        captured)
    (unwind-protect
        (cl-letf (((symbol-function 'jetpacs-org-mode-seed)
                   (lambda () (list :manual file :inbox file)))
                  ((symbol-function 'jetpacs-org-mode--surface)
                   (lambda (owner) (concat "app:" owner)))
                  ((symbol-function 'jetpacs-files-open-path)
                   (lambda (path surface)
                     (setq captured (list path surface))
                     'accepted)))
          (should (eq 'accepted
                      (jetpacs-org-mode--on-open-seed
                       '(:document "manual")
                       '(:surface "app:org-mode"))))
          (should (equal captured (list file "app:jetpacs.files")))
          (should (eq 'rejected
                      (jetpacs-org-mode--on-open-seed
                       '(:document "forged")
                       '(:surface "app:org-mode")))))
      (when-let* ((buffer (get-file-buffer file)))
        (kill-buffer buffer))
      (delete-file file))))

(ert-deftest jetpacs-org-mode-is-a-real-composed-app ()
  "The app claims home, Files, and Habits and installs both Org adapters."
  (let ((entry (assoc jetpacs-org-mode-owner jetpacs-apps--registry)))
    (should entry)
    (should (equal (plist-get (cdr entry) :label) "Org Mode"))
    (should (equal (plist-get (cdr entry) :surfaces)
                   (list jetpacs-org-mode-owner
                         jetpacs-files-owner
                         jetpacs-org-habits-owner)))
    (should (cl-find 'org jetpacs-reader--adapters
                     :key #'jetpacs-reader-adapter-id))
    (should (cl-find 'org jetpacs-editor--adapters
                     :key #'jetpacs-editor-adapter-id))
    (should (memq #'jetpacs-reader--files-body
                  jetpacs-files-editor-body-functions))
    (should (memq #'jetpacs-editor--files-toolbar
                  jetpacs-files-editor-toolbar-functions))
    (should (gethash "org-mode.capture" jetpacs-action-handlers))
    (should (gethash "org-mode.open-seed" jetpacs-action-handlers))
    ;; GR-0: the unverified inline pipeline defaults off.  A dedicated
    ;; owner takes over only after the device cutover ceremony.
    (should-not (memq #'jetpacs-org-mode--sync-reminders
                      jetpacs-shell-after-push-hook))))

(ert-deftest jetpacs-org-mode-legacy-reminder-hook-is-always-inert ()
  "GR-3 never installs the legacy reminder hook, even if its flag is true."
  (unwind-protect
      (progn
        (jetpacs-org-mode-unregister)
        (let ((jetpacs-org-mode-reminders-enabled nil))
          (jetpacs-org-mode-register)
          (should-not (memq #'jetpacs-org-mode--sync-reminders
                            jetpacs-shell-after-push-hook))
          (jetpacs-org-mode-unregister))
        (let ((jetpacs-org-mode-reminders-enabled t))
          (jetpacs-org-mode-register)
          (should-not (memq #'jetpacs-org-mode--sync-reminders
                            jetpacs-shell-after-push-hook))))
    (jetpacs-org-mode-unregister)
    (jetpacs-org-mode-register)))

(provide 'jetpacs-mode-app-test)
;;; jetpacs-mode-app-test.el ends here
